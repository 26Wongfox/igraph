AC_INIT(igraph, 0.2, csardi@rmki.kfki.hu)
AC_CONFIG_SRCDIR(src/games.c)
AM_INIT_AUTOMAKE(igraph, 0.2)
AM_CONFIG_HEADER(config.h)

# Test suite
AC_CONFIG_TESTDIR(tests)
AC_CONFIG_FILES([tests/Makefile tests/atlocal])

AC_PROG_CC
AM_PROG_LEX
AC_PROG_YACC

AC_LIBTOOL_WIN32_DLL
AC_LIBTOOL_DLOPEN
AC_PROG_LIBTOOL
AM_MISSING_PROG([AUTOM4TE], [autom4te])

AC_HEADER_STDC
AC_CHECK_HEADERS([stdlib.h string.h time.h unistd.h])

AC_DEFUN(IGRAPH_WARNING,
[AC_MSG_CHECKING(whether compiler accepts $1)
AC_SUBST(WARNING_CFLAGS)
ac_save_CFLAGS="$CFLAGS"
CFLAGS="$CFLAGS $1"
AC_TRY_COMPILE(,
[int x;],
WARNING_CFLAGS="$WARNING_CFLAGS $1"
AC_MSG_RESULT(yes),
AC_MSG_RESULT(no))
CFLAGS="$ac_save_CFLAGS"])

AC_ARG_ENABLE(gcc-warnings,
              AC_HELP_STRING([--enable-gcc-warnings],
	                     [turn on lots of GCC warnings (not recommended)]),
[case "${enableval}" in
   yes|no) ;;
   *)      AC_MSG_ERROR([bad value ${enableval} for gcc-warnings option]) ;;
 esac],
              [enableval=no])
if test "${enableval}" = yes; then
  IGRAPH_WARNING(-Werror)
  AC_SUBST([WERROR_CFLAGS], [$WARNING_CFLAGS])
  WARNING_CFLAGS=
  IGRAPH_WARNING(-Wall)
  IGRAPH_WARNING(-W)
  IGRAPH_WARNING(-Wbad-function-cast)
  IGRAPH_WARNING(-Wcast-align)
  IGRAPH_WARNING(-Wcast-qual)
  IGRAPH_WARNING(-Wformat)
  IGRAPH_WARNING(-Wmissing-declarations)
  IGRAPH_WARNING(-Wmissing-prototypes)
  IGRAPH_WARNING(-Wnested-externs)
  IGRAPH_WARNING(-Wshadow)
  IGRAPH_WARNING(-Wstrict-prototypes)
  IGRAPH_WARNING(-Wwrite-strings)
fi

use_gprof=no
AC_ARG_ENABLE(profiling,
              AC_HELP_STRING([--enable-profiling], [Enable gprof profiling]),
              [use_gprof=$enableval], [use_gprof=no])

use_python=yes
AC_ARG_ENABLE(python,
              AC_HELP_STRING([--disable-python], [Disable Python interface]),
              [use_python=$enableval], [use_python=yes])
	      
debug=no
AC_ARG_ENABLE(debug,
              AC_HELP_STRING([--enable-debug], [Enable debug build]),
	      [debug=$enableval])

graphml_support=yes
AC_ARG_ENABLE(graphml,
              AC_HELP_STRING([--disable-graphml], [Disable support for GraphML format]),
              [graphml_support=$enableval], [graphml_support=yes])
	      
AC_MSG_CHECKING(for Python >= 2.3)
AM_PYTHON_CHECK_VERSION(python, 2.3, [
  HAVE_PYTHON=1
  PYTHON_VERSION=`python -c "import sys; print sys.version[[:3]]"`
  AC_SUBST(PYTHON_VERSION)
  AC_MSG_RESULT([found, ${PYTHON_VERSION}])
], [
  AC_MSG_RESULT(not found -- disabling Python interface)
  HAVE_PYTHON=0
  use_python=no
])

if test $use_python = yes -a $HAVE_PYTHON -gt 0; then
  PYTHON_PREFIX=`python -c "import sys; print sys.prefix.replace('\\\\\\\\','/')"`
  if test ! -z "$PYTHON_PREFIX"; then
    pysitepkgdir="\${prefix}/lib/python$PYTHON_VERSION/site-packages"
    PYTHON_CFLAGS="-I$PYTHON_PREFIX/include/python$PYTHON_VERSION -I$PYTHON_PREFIX/include"
    PYTHON_CPPFLAGS="${PYTHON_CFLAGS}"
    PYTHON_CXXFLAGS="${PYTHON_CFLAGS}"
    AC_SUBST(PYTHON_PREFIX)
    AC_SUBST(PYTHON_CFLAGS)
    AC_SUBST(PYTHON_CPPFLAGS)
    AC_SUBST(PYTHON_CXXFLAGS)
    AC_SUBST(pysitepkgdir)
      
    PYTHON_LIBS="none"
    AC_CHECK_LIB([python$PYTHON_VERSION], [Py_Main], [
      PYTHON_LIBS="-lpython$PYTHON_VERSION"
    ],[],[-L${PYTHON_PREFIX}/lib -L${PYTHON_PREFIX}/libs])
    if test $PYTHON_LIBS == none; then
      PYTHON_LIBS="python`echo $PYTHON_VERSION|sed -e 's/[[^0-9]]//g'`"
      AC_CHECK_LIB(${PYTHON_LIBS}, [Py_Main], [
        PYTHON_LIBS="-l${PYTHON_LIBS}"
      ],[PYTHON_LIBS="none"],[-L${PYTHON_PREFIX}/lib -L${PYTHON_PREFIX}/libs])
    fi
    if test $PYTHON_LIBS == none; then
      AC_MSG_ERROR([Python library not found!])
    fi

    CFLAGS=${PYTHON_CFLAGS}
    CPPFLAGS=${PYTHON_CPPFLAGS}
    AC_CHECK_HEADER([Python.h],[
      AC_SUBST([pythondir], [$PYTHON_PREFIX"/lib/python"$PYTHON_VERSION/site-packages])
      ENABLE_PYTHON_INTERFACE=yes
      AC_SUBST(PYTHON_PREFIX)
      AC_SUBST(PYTHON_INCLUDES)
      AC_SUBST(PYTHON_LIBS)
    ],[
      AC_MSG_RESULT(none found -- disabling Python interface)
      HAVE_PYTHON=0
      use_python=no
    ])
  else
    AC_MSG_RESULT(none found -- disabling Python interface)
    HAVE_PYTHON=0
    use_python=no
  fi
fi
  
AM_CONDITIONAL(USE_PYTHON, test x$use_python = xyes)

HAVE_LIBXML=0
if test $graphml_support = yes; then
  AC_PATH_PROG([XML2CONFIG], [xml2-config], [none])
  if test "$XML2CONFIG" = "none"; then
    graphml_support=no
  else
    XML2_LIBS=`$XML2CONFIG --libs`
    XML2_CFLAGS=`$XML2CONFIG --cflags`
    AC_CHECK_LIB([xml2], [xmlSAXUserParseFile], [
      OLDCFLAGS=${CFLAGS}
      OLDCPPFLAGS=${CPPFLAGS}
      CFLAGS=${XML2_CFLAGS}
      CPPFLAGS=${XML2_CFLAGS}
      AC_CHECK_HEADER([libxml/parser.h], [
        HAVE_LIBXML=1
	AC_DEFINE([HAVE_LIBXML], [1], [Define to 1 if you have the libxml2 libraries installed])
        CFLAGS="${OLDCFLAGS} ${XML2_CFLAGS}"
	CPPFLAGS="${OLDCFLAGS} ${XML2_CFLAGS}"
	AC_SUBST(XML2_LIBS)
	AC_SUBST(XML2_CFLAGS)
      ], [
        graphml_support=no
        CFLAGS=${OLDCFLAGS}
	CPPFLAGS=${OLDCPPFLAGS}
      ])
    ], [
      graphml_support=no
    ])
  fi
fi

if test "$debug" == "yes"; then
  CFLAGS="${CFLAGS} -ggdb -DRC_DEBUG"
  CPPFLAGS="${CFLAGS} -ggdb -DRC_DEBUG"
  CXXFLAGS="${CFLAGS} -ggdb -DRC_DEBUG"
fi

if test "$use_gprof" == "yes"; then
  CFLAGS="${CFLAGS} -pg"
  CPPFLAGS="${CFLAGS} -pg"
  CXXFLAGS="${CFLAGS} -pg"
fi

AC_CONFIG_FILES([Makefile src/Makefile interfaces/Makefile interfaces/python/Makefile igraph.pc debian/Makefile doc/Makefile interfaces/R/Makefile interfaces/R/igraph/configure.in interfaces/R/DESCRIPTION])
AC_OUTPUT

AC_MSG_RESULT([igraph successfully configured.])
AC_MSG_RESULT([  Python interface       -- $use_python])
AC_MSG_RESULT([  GraphML format support -- $graphml_support])
AC_MSG_RESULT([  Debug build            -- $debug])
AC_MSG_RESULT([  Profiling              -- $use_gprof])
