AC_INIT(igraph, 0.2.1, csardi@rmki.kfki.hu)
AC_CONFIG_SRCDIR(src/games.c)
AM_INIT_AUTOMAKE(igraph, 0.2.1)
AM_CONFIG_HEADER(config.h)

# Test suite
AC_CONFIG_TESTDIR(tests)
AC_CONFIG_FILES([tests/Makefile tests/atlocal])

AC_PROG_CC
AM_PROG_LEX
AC_PROG_YACC

AC_LIBTOOL_WIN32_DLL
AC_LIBTOOL_DLOPEN
AC_PROG_LIBTOOL
AM_MISSING_PROG([AUTOM4TE], [autom4te])

AC_HEADER_STDC
AC_CHECK_HEADERS([stdlib.h string.h time.h unistd.h])

AC_DEFUN([IGRAPH_WARNING],
[AC_MSG_CHECKING(whether compiler accepts $1)
AC_SUBST(WARNING_CFLAGS)
ac_save_CFLAGS="$CFLAGS"
CFLAGS="$CFLAGS $1"
AC_TRY_COMPILE(,
[int x;],
WARNING_CFLAGS="$WARNING_CFLAGS $1"
AC_MSG_RESULT(yes),
AC_MSG_RESULT(no))
CFLAGS="$ac_save_CFLAGS"])

AC_DEFUN([IGRAPH_CC_SWITCH],
[AC_MSG_CHECKING(whether compiler supports $1)
ac_save_CFLAGS="$CFLAGS"
CFLAGS="$CFLAGS $1"
AC_TRY_COMPILE(,
[int x;],
AC_MSG_RESULT(yes)
$2,
AC_MSG_RESULT(no)
$3)
CFLAGS="$ac_save_CFLAGS"])

AC_ARG_ENABLE(gcc-warnings,
              AC_HELP_STRING([--enable-gcc-warnings],
	                     [turn on lots of GCC warnings (not recommended)]),
[case "${enableval}" in
   yes|no) ;;
   *)      AC_MSG_ERROR([bad value ${enableval} for gcc-warnings option]) ;;
 esac],
              [enableval=no])
if test "${enableval}" = yes; then
  IGRAPH_WARNING(-Werror)
  AC_SUBST([WERROR_CFLAGS], [$WARNING_CFLAGS])
  WARNING_CFLAGS=
  IGRAPH_WARNING(-Wall)
  IGRAPH_WARNING(-W)
  IGRAPH_WARNING(-Wbad-function-cast)
  IGRAPH_WARNING(-Wcast-align)
  IGRAPH_WARNING(-Wcast-qual)
  IGRAPH_WARNING(-Wformat)
  IGRAPH_WARNING(-Wmissing-declarations)
  IGRAPH_WARNING(-Wmissing-prototypes)
  IGRAPH_WARNING(-Wnested-externs)
  IGRAPH_WARNING(-Wshadow)
  IGRAPH_WARNING(-Wstrict-prototypes)
  IGRAPH_WARNING(-Wwrite-strings)
fi

use_gprof=no
AC_ARG_ENABLE(profiling,
              AC_HELP_STRING([--enable-profiling], [Enable gprof profiling]),
              [use_gprof=$enableval], [use_gprof=no])

use_python=yes
AC_ARG_ENABLE(python,
              AC_HELP_STRING([--disable-python], [Disable Python interface]),
              [use_python=$enableval], [use_python=yes])
	      
debug=no
AC_ARG_ENABLE(debug,
              AC_HELP_STRING([--enable-debug], [Enable debug build]),
	      [debug=$enableval])

graphml_support=yes
AC_ARG_ENABLE(graphml,
              AC_HELP_STRING([--disable-graphml], [Disable support for GraphML format]),
              [graphml_support=$enableval], [graphml_support=yes])

HAVE_PYTHON=1
if test $use_python == yes; then
  AM_PATH_PYTHON(2.3, [
    HAVE_PYTHON=1
    # MinGW returns Python path with backslashes, replace them with
    # forward slashes
    PYTHON_INCLUDES=`echo $PYTHON_INCLUDES | tr '\\\\' '/'`
    pyexecdir=`$PYTHON -c "from distutils import sysconfig; print sysconfig.get_python_lib(1,0)" | tr '\\\\' '/'`
  ], [
    HAVE_PYTHON=0
    use_python=no
  ])
fi

if test $use_python = yes -a $HAVE_PYTHON -gt 0; then
  PYTHON_CFLAGS="-I"`$PYTHON -c "from distutils import sysconfig; print sysconfig.get_python_inc()" | tr '\\\\' '/'`
  PYTHON_CPPFLAGS="${PYTHON_CFLAGS}"
  PYTHON_CXXFLAGS="${PYTHON_CFLAGS}"
  PYTHON_LDFLAGS="-no-undefined"
  AC_SUBST(PYTHON_CFLAGS)
  AC_SUBST(PYTHON_CPPFLAGS)
  AC_SUBST(PYTHON_CXXFLAGS)
  AC_SUBST(PYTHON_LDFLAGS)

  # These code snippets are taken from the Apache mod_python's configure.in
  # this is where the Python libraries will get installed
  AC_SUBST(PY_STD_LIB)
  AC_MSG_CHECKING(where Python libraries are installed)
  PY_STD_LIB=`$PYTHON -c 'import distutils.sysconfig; print distutils.sysconfig.get_python_lib(plat_specific=1, standard_lib=1)' | tr '\\\\' '/'`
  AC_MSG_RESULT($PY_STD_LIB)
  
  PyLIBP=${PY_STD_LIB}
  
  # Some platforms have the Python libraries in a separate config subdir
  # but others not. Choose the config directory only if it exists
  if test -d ${PyLIBP}/config; then
    PyLIBPL=${PyLIBP}/config
  else
    PyLIBPL=${PyLIBP}
  fi
  
  # The standard Windows installation of Python does not have a dot in
  # the version number when used in the library filename
  PyPYTHONLIBS=${PyLIBPL}/libpython${PYTHON_VERSION}.a
  if test ! -f $PyPYTHONLIBS; then
    PYTHON_VERSION=`echo $PYTHON_VERSION | tr -d '.'`
    PyPYTHONLIBS=${PyLIBPL}/libpython${PYTHON_VERSION}.a
  fi

  if test -f ${PyLIBPL}/Makefile; then  
    PyLIBS=`grep "^LIB[[SMC]]=" ${PyLIBPL}/Makefile | cut -f2 -d= | tr '\011\012\015' '   '`
    PyMODLIBS=`grep "^LOCALMODLIBS=" ${PyLIBPL}/Makefile | cut -f2 -d= | tr '\011\012\015' '   '`
  fi
  
  AC_CHECK_LIB([python${PYTHON_VERSION}], Py_NewInterpreter,
    [PyPYTHONLIBS="-lpython${PYTHON_VERSION}"],
    [
      IGRAPH_CC_SWITCH([-undefined dynamic_lookup], [
        # Mac OS X doesn't have libpython*.a but supports dynamic lookup
        PyPYTHONLIBS=""
        PyLIBS="-undefined dynamic_lookup -module"
        PyMODLIBS=""
        PYTHON_LDFLAGS="-undefined dynamic_lookup -module"
      ], [
        if test -f ${PyLIBPL}/libpython${PYTHON_VERSION}.a; then
          PyPYTHONLIBS=${PyLIBPL}/libpython${PYTHON_VERSION}.a
        else
          AC_ERROR([cannot link to Python and no dynamic lookup available])
        fi
      ])
    ], [ ${PyLIBS} ${PyMODLIBS} ])
    
  PYTHON_LIBS="${PyPYTHONLIBS} ${PyLIBS} ${PyMODLIBS}"

  CFLAGS=${PYTHON_CFLAGS}
  CPPFLAGS=${PYTHON_CPPFLAGS}
  AC_CHECK_HEADER([Python.h],[
    ENABLE_PYTHON_INTERFACE=yes
    AC_SUBST(PYTHON_PREFIX)
    AC_SUBST(PYTHON_INCLUDES)
    AC_SUBST(PYTHON_LIBS)
  ],[
    HAVE_PYTHON=0
    use_python=no
  ])
else
  AC_MSG_RESULT(none found -- disabling Python interface)
  HAVE_PYTHON=0
  use_python=no
fi
  
AM_CONDITIONAL(USE_PYTHON, test x$use_python = xyes)

HAVE_LIBXML=0
if test $graphml_support = yes; then
  AC_PATH_PROG([XML2CONFIG], [xml2-config], [none])
  if test "$XML2CONFIG" = "none"; then
    graphml_support=no
  else
    XML2_LIBS=`$XML2CONFIG --libs`
    XML2_CFLAGS=`$XML2CONFIG --cflags`
    AC_CHECK_LIB([xml2], [xmlSAXUserParseFile], [
      OLDCFLAGS=${CFLAGS}
      OLDCPPFLAGS=${CPPFLAGS}
      CFLAGS=${XML2_CFLAGS}
      CPPFLAGS=${XML2_CFLAGS}
      AC_CHECK_HEADER([libxml/parser.h], [
        HAVE_LIBXML=1
	AC_DEFINE([HAVE_LIBXML], [1], [Define to 1 if you have the libxml2 libraries installed])
        CFLAGS="${OLDCFLAGS} ${XML2_CFLAGS}"
	CPPFLAGS="${OLDCFLAGS} ${XML2_CFLAGS}"
	AC_SUBST(XML2_LIBS)
	AC_SUBST(XML2_CFLAGS)
      ], [
        graphml_support=no
        CFLAGS=${OLDCFLAGS}
	CPPFLAGS=${OLDCPPFLAGS}
      ])
    ], [
      graphml_support=no
    ])
  fi
fi

if test "$debug" == "yes"; then
  CFLAGS="${CFLAGS} -ggdb -DRC_DEBUG"
  CPPFLAGS="${CFLAGS} -ggdb -DRC_DEBUG"
  CXXFLAGS="${CFLAGS} -ggdb -DRC_DEBUG"
fi

if test "$use_gprof" == "yes"; then
  CFLAGS="${CFLAGS} -pg"
  CPPFLAGS="${CFLAGS} -pg"
  CXXFLAGS="${CFLAGS} -pg"
fi

AC_CONFIG_FILES([Makefile src/Makefile interfaces/Makefile interfaces/python/Makefile igraph.pc debian/Makefile doc/Makefile doc/book/Makefile interfaces/R/igraph/configure.in interfaces/R/Makefile interfaces/R/DESCRIPTION])
AC_OUTPUT

AC_MSG_RESULT([igraph successfully configured.])
AC_MSG_RESULT([  Python interface       -- $use_python])
AC_MSG_RESULT([  GraphML format support -- $graphml_support])
AC_MSG_RESULT([  Debug build            -- $debug])
AC_MSG_RESULT([  Profiling              -- $use_gprof])
